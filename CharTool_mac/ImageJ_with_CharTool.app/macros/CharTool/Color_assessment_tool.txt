// Color Assessment Tool
//
// This text file contains all of the tools necessary to assess the mean RGB color values of 
// a sediment sample using imageJ and a usb connected digital microscope
//  
// Grant Snitker
// Department of Crop and Soil Sciences
// School of Agricultural and Environmental Sciences
// University of Georgia
// grant.snitker@uga.edu
// 2019

var samplenum = "";
var depths =  "";
var date =  "";
var analyst = "";
var notes =  "";


    macro "Assess Average RBG Color  [r]" {
       Dialog.create("CharTool - Enter Metadata");
       Dialog.addString("      Sample Number:", "Enter Sample Number", 20); 
       Dialog.addString("      Depth Range (cm):", "Enter Depth Range", 20); 
       Dialog.addString("      Date:", "Date of Analysis", 20); 
       Dialog.addString("      Analyst:", "Analyst Name or Initials", 20); 
       Dialog.addString("      Other Info:", "Other Pertinent Information", 20); 
       Dialog.show;
       samplenum =  Dialog.getString();
       depths =  Dialog.getString();
       date =  Dialog.getString();
       analyst =  Dialog.getString();
       notes =  Dialog.getString();
       
     setRGBWeights(1, 0, 0);
     List.setMeasurements; 
     red = List.getValue("Mean");
     setRGBWeights(0, 1, 0);
     List.setMeasurements; 
     green = List.getValue("Mean");
     setRGBWeights(0, 0, 1);
     List.setMeasurements; 
     blue = List.getValue("Mean");
     setResult("Sample", nResults, samplenum);
	 setResult("Depths", nResults-1, depths);
	 setResult("Date", nResults-1, date);
	 setResult("Analyst", nResults-1, analyst);
	 setResult("Other info", nResults-1, notes);
     setResult("Red", nResults-1, red);
     setResult("Green", nResults-1, green);    
     setResult("Blue", nResults-1, blue);    
     updateResults;
        }

    macro "Remove Last Measurement  [d]" {
        IJ.deleteRows(nResults-1,nResults-1);
         }

    macro "Open Instructions  [h]" {
       Dialog.create("CharTool");
        Dialog.addMessage("Press [R] to enter sample number and measure mean RBG values");
        Dialog.addMessage("Press [D] to delete last measurement");
        Dialog.addMessage("Press [W] to reset scale");
        Dialog.addMessage("Press [H] to see these instructions again.");
       Dialog.show;
        }
    

  macro "Open New Image  [q]" {
       waitForUser("CharTool", "Opening new image");
       open();
        }
    
        
 macro "Reset scale [w]" {
        setTool("line");
       waitForUser("Set Scale", "Use Draw Line tool to select a known length\nClick OK when finished");
       run("Set Scale...");
       Dialog.create("CharTool");
       Dialog.addMessage("Add visible scale bar to images?");
       items = newArray("Yes", "No");
       Dialog.addChoice("                  ", items, "Yes");
       Dialog.show;
       choice1c = Dialog.getChoice();
       if (choice1c == "Yes") {
       
        Dialog.create("CharTool");
       Dialog.addString("Length of scale bar", "length in units", 15); 
        Dialog.show;
       scalebar =  Dialog.getString();
       
       run("Scale Bar...", "width=scalebar height=6 font=16 color=Green background=None location=[Lower Left] overlay");
       }
       if (choice1c == "No") {};
        }